// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/09/circle/circleGame.jack

/**
 * Implements the circle Dance game.
 * This simple game allows the user to move a black circle around
 * the screen, and change the circle's size during the movement.
 * When the game starts, a circle of 30 by 30 pixels is shown at the
 * top-left corner of the screen. The user controls the circle as follows.
 * The 4 arrow keys are used to move the circle up, down, left, and right.
 * The 'z' and 'x' keys are used, respectively, to decrement and increment
 * the circle's size. The 'q' key is used to quit the game.
 */

class CircleGame {
   field Circle circle; // the circle of this game
   field int direction; // the circle's current direction: 
                        // 0=none, 1=up, 2=down, 3=left, 4=right

   /** Constructs a new circle Game. */
   constructor CircleGame new() {
      // Creates a 30 by 30 pixels circle and positions it at the top-left
      // of the screen.
      let circle = Circle.new(15, 15, 15);
      let direction = 0;  // initial state is no movement
      return this;
   }

   /** Disposes this game. */
   method void dispose() {
      do circle.dispose();
      do Memory.deAlloc(this);
      return;
   }

   /** Moves the circle in the current direction. */
   method void movecircle() {
      if (direction = 1) { do circle.moveUp(); }
      if (direction = 2) { do circle.moveDown(); }
      if (direction = 3) { do circle.moveLeft(); }
      if (direction = 4) { do circle.moveRight(); }
      do Sys.wait(5);  // delays the next movement
      return;
   }

   /** Runs the game: handles the user's inputs and moves the circle accordingly */
   method void run() {
      var char key;  // the key currently pressed by the user
      var boolean exit;
      let exit = false;
      
      while (~exit) {
         // waits for a key to be pressed
         while (key = 0) {
            let key = Keyboard.keyPressed();
            do movecircle();
         }
         if (key = 81)  { let exit = true; }     // q key
         if (key = 90)  { do circle.decSize(); } // z key
         if (key = 88)  { do circle.incSize(); } // x key
         if (key = 131) { let direction = 1; }   // up arrow
         if (key = 133) { let direction = 2; }   // down arrow
         if (key = 130) { let direction = 3; }   // left arrow
         if (key = 132) { let direction = 4; }   // right arrow

         // waits for the key to be released
         while (~(key = 0)) {
            let key = Keyboard.keyPressed();
            if (key = 90)  { do circle.decSize(); } // z key
            if (key = 88)  { do circle.incSize(); } // x key
            do movecircle();
         }
     } // while
     return;
   }
}



